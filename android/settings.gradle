pluginManagement {
    def flutterSdkPath = {
        def properties = new Properties()
        file("local.properties").withInputStream { properties.load(it) }
        def flutterSdkPath = properties.getProperty("flutter.sdk")
        assert flutterSdkPath != null, "flutter.sdk not set in local.properties"
        return flutterSdkPath
    }()

    includeBuild("$flutterSdkPath/packages/flutter_tools/gradle")

    repositories {
        // For network environments where Google Maven/Plugin endpoints are blocked,
        // allow resolving Android/Gradle plugins via a mirror.
        // Keep `google()` as well for normal networks.
        maven { url "https://maven.aliyun.com/repository/google" }
        maven { url "https://maven.aliyun.com/repository/gradle-plugin" }
        maven { url "https://maven.aliyun.com/repository/public" }
        google()
        mavenCentral()
        gradlePluginPortal()
    }
}


plugins {
    id "dev.flutter.flutter-plugin-loader" version "1.0.0"
    id "com.android.application" version "8.4.0" apply false
    id "org.jetbrains.kotlin.android" version "1.9.22" apply false
}
dependencyResolutionManagement {
    // Flutter's Gradle plugins may add repositories at runtime; failing the build
    // here prevents `flutter run` from working.
    repositoriesMode.set(RepositoriesMode.PREFER_SETTINGS)
    repositories {
        // --- 阿里云镜像 (依赖) ---
        maven { url 'https://maven.aliyun.com/repository/google' }
        maven { url 'https://maven.aliyun.com/repository/public' }
        // -----------------------
        // Flutter Android embedding artifacts (io.flutter:*)
        // Default host is often blocked; include CFUG mirror first.
        maven { url 'https://storage.flutter-io.cn/download.flutter.io' }
        maven { url 'https://storage.googleapis.com/download.flutter.io' }
        google()
        mavenCentral()
    }
}
include ":app"
